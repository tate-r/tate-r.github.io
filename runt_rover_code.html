<!DOCTYPE html>
<html>
<head>
<title>"Runt Rover" Arduino Code</title>
</head>
<body>
<h2 style="text-align:center;">"Runt Rover" Arduino Code</h2>
<h4>An Arduino Processing Sketch to Power a Robot</h4>
<br>
<a href="http://tate-r.github.io">Return to Main Page</a>
<br>
<br>
<pre>
/* 
This is built from the test sketch for the Adafruit Motor Shield for Arduino v2

For use with the Adafruit Motor Shield v2 
---->	http://www.adafruit.com/products/1438
*/

#include "Wire.h"
#include "Adafruit_MotorShield.h"
#include "utility/Adafruit_MS_PWMServoDriver.h"

// Create the motor shield object with the default I2C address
Adafruit_MotorShield theMotorShield = Adafruit_MotorShield(); 
// Or, create it with a different I2C address (say for stacking)
// Adafruit_MotorShield theMotorShield = Adafruit_MotorShield(0x61); 

// Define an array of motor pointers to  M1, M2, M3 and M4. 
Adafruit_DCMotor *motorArray[4] = {theMotorShield.getMotor(1), theMotorShield.getMotor(2), theMotorShield.getMotor(3), theMotorShield.getMotor(4)};

// You can also make another motor on port M2
//Adafruit_DCMotor *myOtherMotor = theMotorShield.getMotor(2);

//This is the way the motors are wired right now.  
//If you change which motor is connected to what shield outlet, you need to change these
#define LEFT_FRONT  0
#define RIGHT_FRONT 1
#define LEFT_REAR   2
#define RIGHT_REAR  3

//Channels here are channels on the receiver (per measurements)
#define CHANNEL_2_PIN 8
#define CHANNEL_1_PIN 9


void setup() 
{
  Serial.begin(9600);           // set up Serial library at 9600 bps
  Serial.println("Caden's Jr. Robot!");

  theMotorShield.begin();  // create with the default frequency 1.6KHz
  //theMotorShield.begin(1000);  // OR with a different frequency, say 1KHz

  pinMode(CHANNEL_2_PIN, INPUT);
  pinMode(CHANNEL_1_PIN, INPUT);

  allForward();
}


//  LEFT_FRONT  <>---------<>  RIGHT_FRONT
//                <>-----<>  
//                <>-----<>  
//                <>-----<>  
//                <>-----<>  
//                <>-----<>  
//  LEFT_REAR   <>---------<>  RIGHT_REAR

#define WHICH_MOTOR RIGHT_REAR

#define FWD_BKWD_SLOPE -0.282
#define FWD_BKWD_INTERCEPT 548.5
#define FWD_BKWD_THRESHOLD 10.0

#define MAX_SPEED 254
#define L_R_SLOPE  0.288
#define L_R_INTERCEPT -426.7
#define L_R_THRESHOLD 15.0

int currentSpeed;

void loop() 
{
  uint8_t i, j;
  int channel_2_Out;
  int channel_1_Out;
  int leftAdjustment = 0;
  int rightAdjustment = 0;
  int adjustedSpeed = 0;
  float theSpeed = 0.0;  
  float left_right = 0.0;

  channel_2_Out = pulseIn(CHANNEL_2_PIN, HIGH);
  channel_1_Out = pulseIn(CHANNEL_1_PIN, HIGH, 25000);

  theSpeed = FWD_BKWD_SLOPE * channel_2_Out + FWD_BKWD_INTERCEPT;
  left_right = L_R_SLOPE * channel_1_Out + L_R_INTERCEPT;
  
  if (theSpeed < FWD_BKWD_THRESHOLD)
    theSpeed = 0.0;

  if (abs(left_right) < L_R_THRESHOLD)
    left_right = 0.0;

  if (theSpeed > MAX_SPEED)
    theSpeed = MAX_SPEED;

    //Here, we are mapping the old linear ajdustment over to the new controller on the right
  currentSpeed = map(theSpeed, 0, 256, -256, 256);
//  currentSpeed = theSpeed;
  
  Serial.print("theSpeed is ");
  Serial.println(currentSpeed);

  if (currentSpeed > 0 && abs(currentSpeed) > FWD_BKWD_THRESHOLD)
    {
    allForward();
    
    if (left_right == 0)
      {    
      setAllSpeeds(currentSpeed,                      //Left Front
                    currentSpeed,                     //Right Front
                    currentSpeed,                     //Left Rear
                    currentSpeed);                    //Right Rear
      }
    else if (left_right > 0)  //Right turn
      {
      leftAdjustment = abs(left_right);

        //Adjust the speed of one side of the robot
        //proportional to the amount of steering to that side
      adjustedSpeed = currentSpeed - leftAdjustment;

        //Be sure to avoid sending a signal of a too high speed...
      if (adjustedSpeed > MAX_SPEED)
        adjustedSpeed = MAX_SPEED;
        
        //Avoid sending a signal of negative speed...
      if (adjustedSpeed <= 0)
        adjustedSpeed = 0;
  
      setAllSpeeds(currentSpeed,    //Left Front
                    adjustedSpeed,    //Right Front
                    currentSpeed,   //Left Rear
                    adjustedSpeed);   //Right Rear
      }
    else if (left_right < 0)  //Left turn
      {
      rightAdjustment = abs(left_right);

      adjustedSpeed = currentSpeed - rightAdjustment;

        //Be sure to avoid sending a signal of a too high speed...
      if (adjustedSpeed > MAX_SPEED)
        adjustedSpeed = MAX_SPEED;

        //Avoid sending a signal of negative speed...
      if (adjustedSpeed <= 0)
        adjustedSpeed = 0;

      setAllSpeeds(adjustedSpeed,     //Left Front
                    currentSpeed,   //Right Front
                    adjustedSpeed,    //Left Rear
                    currentSpeed);  //Right Rear
      }
    }
  else if (abs(currentSpeed) > FWD_BKWD_THRESHOLD && currentSpeed < 0)
    {
      allBackward();

      if (abs(currentSpeed) > MAX_SPEED)
        currentSpeed = MAX_SPEED;
      
      setAllSpeeds(abs(currentSpeed),                    //Left Front
                    abs(currentSpeed),                   //Right Front
                    abs(currentSpeed),                   //Left Rear
                    abs(currentSpeed));                  //Right Rear
      
    }
  else if (abs(left_right) > L_R_THRESHOLD*2)
    {
      if(left_right < 0)
        rotateLeft();
      else  
        rotateRight();

//      setAllSpeeds(abs(left_right),                    //Left Front
//                    abs(left_right),                   //Right Front
//                    abs(left_right),                   //Left Rear
//                    abs(left_right));                  //Right Rear
      setAllSpeeds(64,                    //Left Front
                    64,                   //Right Front
                    64,                   //Left Rear
                    64);                  //Right Rear
    }
  else if (left_right == 0 && abs(currentSpeed) <= FWD_BKWD_THRESHOLD )
    {
      allRelease();
    }
    
//  Serial.print("tech");
//  allRelease();
//  delay(1000);
}


void setAllSpeeds(int LF_speed, int RF_speed, int LR_speed, int RR_speed)
{
  motorArray[LEFT_FRONT]->setSpeed(LF_speed);  
  motorArray[RIGHT_FRONT]->setSpeed(RF_speed);  
  motorArray[LEFT_REAR]->setSpeed(LR_speed);  
  motorArray[RIGHT_REAR]->setSpeed(RR_speed);  
}

void allForward()
{
    motorArray[LEFT_FRONT]->run(FORWARD);
    motorArray[RIGHT_FRONT]->run(FORWARD);
    motorArray[LEFT_REAR]->run(FORWARD);
    motorArray[RIGHT_REAR]->run(FORWARD);
}

void allBackward()
{
    motorArray[LEFT_FRONT]->run(BACKWARD);
    motorArray[RIGHT_FRONT]->run(BACKWARD);
    motorArray[LEFT_REAR]->run(BACKWARD);
    motorArray[RIGHT_REAR]->run(BACKWARD);
}

void allRelease()
{
    motorArray[LEFT_FRONT]->run(RELEASE);
    motorArray[RIGHT_FRONT]->run(RELEASE);
    motorArray[LEFT_REAR]->run(RELEASE);
    motorArray[RIGHT_REAR]->run(RELEASE);
}

void rotateLeft()
{
//    motorArray[LEFT_FRONT]->run(BACKWARD);
//    motorArray[RIGHT_FRONT]->run(FORWARD);
//    motorArray[LEFT_REAR]->run(BACKWARD);
//    motorArray[RIGHT_REAR]->run(FORWARD);
    motorArray[LEFT_FRONT]->run(RELEASE);
    motorArray[RIGHT_FRONT]->run(FORWARD);
    motorArray[LEFT_REAR]->run(RELEASE);
    motorArray[RIGHT_REAR]->run(FORWARD);
}

void rotateRight()
{
    motorArray[LEFT_FRONT]->run(FORWARD);
    motorArray[RIGHT_FRONT]->run(BACKWARD);
    motorArray[LEFT_REAR]->run(FORWARD);
    motorArray[RIGHT_REAR]->run(BACKWARD);
}


</pre>
<a href="http://tate-r.github.io">Return to Main Page</a>
</body>
</html>
